apply plugin: 'java'
apply plugin: 'eclipse'
apply from: 'dependencies.gradle'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile libs.pi4j
    compile libs.logging     
    testCompile libs.test
}

task fatJar(type: Jar) {
    //fatjar + client programs

    from(sourceSets.main.output)
    manifest {
        attributes 'Implementation-Title': 'Stepper Motor Controller FatJar',
            'Implementation-Version': version,
            'Main-Class': 'org.jason.steppercontroller.client.MotorCommand'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

jar {
    from(sourceSets.main.output)
    manifest {
        attributes 'Implementation-Title': 'Stepper Motor Controller',
            'Implementation-Version': version
    }
    baseName = project.name
    //no client programs
    exclude "org/jason/steppercontroller/client/**"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

test {
    useTestNG() {
        useDefaultListeners = true
        suites 'src/test/resources/testng.xml'
    }
}